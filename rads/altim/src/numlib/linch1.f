C    ****************************************************************** 
C                                                      33A. LINCH1
C                                                                       
C    PURPOSE                                                            
C    TO SOLVE A SYSTEM OF LINEAR EQUATIONS AX=B BY CHOLESKI'S METHOD
C    WHERE A IS A SYMMETRIC MATRIX IN VECTOR FORM and already transformed
C    using the MATCHO subroutine.
C                                                                       
C    USAGE                                                              
C    CALL LINCH1 (A,H,X,KP,N)
C                                                                       
C    DESCRIPTION OF PARAMETERS                                          
C    A    I REAL VECTOR CONTAINING THE COLUMS OF THE UPPER PART OF
C           THE SYMMETRIC MATRIX A transformed using the Choleski reduction.
C           THE DIMENSION IS AT LEAST N*(N+1)/2
C           A CAN BE FORMED BY running SUBROUTINE MATSYD and MATCHO.
C    H    I INTEGER VECTOR WITH H(I)=I*(I-1)/2
C           H CAN BE FILLED WITH MATSY1
C    X    I RIGHT HAND SIDE B
C         O SOLUTION VECTOR X
C    KP   I PERMUTATION VECTOR OF DIMENSION N, GENERATED BY MATCHO
C    N    I DIMENSION OF SYMMETRIX MATRIX A IS (N,N)
C
C    *******************************************************************
      SUBROUTINE LINCH1(A,H,X,KP,N)
      IMPLICIT REAL*8 (A-H,O-Z)
      INTEGER H(N)
      DIMENSION A(*),X(N),KP(N)
      DO 10 I=1,N
         J=KP(I)
         IF (I.NE.J) THEN
            T=X(I)
            X(I)=X(J)
            X(J)=T
         ENDIF
         II=H(I)
         DO 10 J=I-1,1,-1
   10       X(I)=X(I)-A(J+II)*X(J)
      DO 20 I=1,N
   20    X(I)=X(I)/A(I+H(I))
      DO 30 I=N-1,1,-1
         DO 30 J=I+1,N
   30       X(I)=X(I)-A(I+H(J))*X(J)
      DO 40 J=N,1,-1
         I=KP(J)
         IF (I.NE.J) THEN
            T=X(I)
            X(I)=X(J)
            X(J)=T
         ENDIF
   40 CONTINUE
      END
