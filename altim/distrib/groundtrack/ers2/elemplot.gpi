set term post land enhanced "Helvetica" 12
set output "elemplot.ps"
set data style dots
set format x "%Y"
set xdata time
f(x,y)=((x+y)/2-51544)*86400
set multiplot
set lmargin 0
set rmargin 0
set bmargin 0
set tmargin 0
set size 0.90,0.22
set linestyle 1 lt -1 lw 2
set xrange [*:84067200]
set x2range [*:84067200]

set origin 0.05,0.04
set yrange [89:95]
set ytics 1
set label 1 "argument of perigee (deg)" at graph 0.03,0.85
plot "orbelem.out" using (f($2,$3)):10 notitle ls 1

set format x ""
set yrange [*:*]
set origin 0.05,0.54
set ytics 0.05
set label 1 "eccentricity (10^{-3})" at graph 0.03,0.85
plot "orbelem.out" using (f($2,$3)):($6*1000) notitle ls 1

set origin 0.05,0.29
set ytics 0.01
set label 1 "inclination (deg)" at graph 0.03,0.85
plot "orbelem.out" using (f($2,$3)):7 notitle ls 1

set format x2 "%Y"
set x2tics mirror
set x2data time
set origin 0.05,0.79
set ytics 50
set label 1 "semi-major axis 7163000 + (m)" at graph 0.03,0.85
plot "orbelem.out" using (f($2,$3)):($5-7163000) notitle ls 1
set nomultiplot
